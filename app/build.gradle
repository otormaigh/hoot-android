import hoot.*

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
if (file('../signing/play.json').exists()) {
  apply plugin: 'com.github.triplet.play'
  play {
    defaultToAppBundles = true
    serviceAccountCredentials = file("../signing/play.json")
    track = 'internal'
  }
}
if (file('../enc.gradle.properties').exists()) {
  apply from: '../enc.gradle.properties'
}

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "ie.pennylabs.hoot"
    minSdkVersion 27
    targetSdkVersion 28
    versionCode BuildConst.Version.code
    versionName BuildConst.Version.name
    archivesBaseName = "hoot-$versionName"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }

    buildConfigField "String", "IMGUR_URL", "\"https://api.imgur.com/3/\""
    buildConfigField "String", "IMGUR_CLIENT_ID", project.properties["imgur_client_id"]
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  signingConfigs {
    debug {
      storeFile file('../signing/debug.keystore')
    }
    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file(Keystore.RELEASE_STORE)
        storePassword Keystore.STORE_PASSWORD
        keyAlias Keystore.KEY_ALIAS
        keyPassword Keystore.KEY_PASSWORD
      }
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix '.debug'
    }

    release {
      if (file('../signing/release.keystore').exists()) {
        signingConfig signingConfigs.release
      } else {
        signingConfig signingConfigs.debug
      }
      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = true
      }
    }
  }

  packagingOptions {
    exclude 'META-INF/main.kotlin_module'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Deps.kotlin"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$Deps.coroutines"

  implementation "com.google.android.material:material:$Deps.androidx"
  implementation "androidx.appcompat:appcompat:$Deps.androidx"
  implementation "androidx.core:core-ktx:$Deps.androidx"
  implementation "androidx.recyclerview:recyclerview:$Deps.androidx"
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
  implementation "androidx.room:room-runtime:$Deps.arch_room"
  kapt "androidx.room:room-compiler:$Deps.arch_room"

  implementation 'com.jakewharton.timber:timber:4.7.1'

  implementation 'com.google.firebase:firebase-analytics:16.0.4'
  implementation 'com.google.firebase:firebase-core:16.0.4'
  implementation 'com.google.firebase:firebase-perf:16.1.2'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

  implementation "com.squareup.retrofit2:retrofit:$Deps.retrofit2"
  implementation "com.squareup.retrofit2:converter-moshi:$Deps.retrofit2"
  implementation "com.squareup.okhttp3:okhttp:$Deps.okhttp3"
  implementation "com.squareup.okhttp3:logging-interceptor:$Deps.okhttp3"
  implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.13.0-eap13'

  androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
  androidTestImplementation "androidx.room:room-testing:$Deps.arch_room"
  androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'
}

androidExtensions {
  experimental = true
}

task prepareNextRelease {
  description "Prepares the project for the next release version, see: RELEASING.md"

  doLast {
    def new_version = project.android.defaultConfig.versionName

    // 7. Create a new release branch `git checkout -b release-{versionName}`
    exec { commandLine 'git', 'checkout', '-b', "release-$new_version" }

    exec {
      // 8. Update previous `CHANGELOG.md` entry to append title with the build commit of that release
      commandLine 'sed', '-i', "", '-e', "1 s/\$/ - ${BuildConst.Git.shortHash}/", file('../CHANGELOG.md')
    }

    // 9. Commit changes `git commit -am 'bump version to {versionName}'`
    exec { commandLine 'git', 'commit', '-am', "bump version to $new_version" }
  }
}

task generateChangelog {
  description "Generate a changelog based on the range of commits that triggered a build on CircleCI."

  doLast {
    exec {
      def commitRange =  System.getenv("CIRCLE_COMPARE_URL").split("/").last()
      if (commitRange.isEmpty()) commitRange = System.getenv("CIRCLE_SHA1")
      if (!commitRange.contains("...")) commitRange = "HEAD^..$commitRange"
      println("COMMIT_RANGE -> $commitRange")

      commandLine 'git', 'log', '--pretty=%s', "$commitRange"
      standardOutput = new FileOutputStream('app/src/main/play/release-notes/en-GB/internal.txt')
    }
  }
}

if (file('google-services.json').exists()) {
  apply plugin: 'io.fabric'
  apply plugin: 'com.google.firebase.firebase-perf'
  apply plugin: 'com.google.gms.google-services'
}